plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'org.jlleitschuh.gradle.ktlint' version '8.2.0'
    id 'org.jlleitschuh.gradle.ktlint-idea' version '8.2.0'
}

group = 'io.github.dylmeadows'
version = '2.0.2-SNAPSHOT'

ext.testfx_version = '4.0.13-alpha'
ext.jackson_version = '2.9.9'
ext.javafx_version = '11.0.2'
ext.javafx_modules = [
    'javafx-base',
    'javafx-graphics',
    'javafx-controls',
    'javafx-media',
    'javafx-fxml'
]

apply from: 'gradle/javafx.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven { url "https://oss.jfrog.org/artifactory/libs-snapshot" }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.1.1"
    implementation("io.github.dylmeadows:spring-boot-starter-javafx:2.1.6-SNAPSHOT") { changing = true }
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"

    implementation "de.jensd:fontawesomefx:8.9"

    testImplementation "junit:junit:4.12"
    testImplementation "org.testfx:testfx-core:$testfx_version"
    testImplementation "org.testfx:testfx-junit:$testfx_version"
}

configurations.compile {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

application {
    mainClassName = "io.github.dylmeadows.eontimer.AppLauncher"
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}
